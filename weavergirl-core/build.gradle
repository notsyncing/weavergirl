import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath "io.github.notsyncing.weavergirl:weavergirl-gradle-plugin:0.1.0"
    }
}

plugins {
    id "de.richsource.gradle.plugins.typescript" version "1.8.0"
}

apply plugin: "io.github.notsyncing.weavergirl.gradle"

compileTypeScript {
    sourcemap = true
    target = "es6"
    pretty = true

    if (Os.isFamily(Os.FAMILY_MAC)) {
        compilerExecutable = "/usr/local/bin/node /usr/local/bin/tsc"
    }
}

weavergirl {
    srcDir = null
    additionalFiles = [project.buildDir.toPath().resolve("ts/weavergirl-core.js").toString()]
}

task webpack() {
    def tsOutPath = project.buildDir.toPath().resolve("ts")

    copy {
        from "webpack.config.js"
        into tsOutPath.toFile()
    }

    doLast {
        exec {
            workingDir tsOutPath.toFile()

            if (Os.isFamily(Os.FAMILY_MAC)) {
                commandLine "/usr/local/bin/node", "/usr/local/bin/npm", "link", "webpack", "babel-loader", "babel-core", "babel-preset-es2015", "babel-preset-env", "babel-polyfill"
            } else {
                commandLine "npm", "link", "webpack", "babel-loader", "babel-core", "babel-preset-es2015", "babel-preset-env", "babel-polyfill"
            }
        }

        exec {
            workingDir tsOutPath.toFile()

            if (Os.isFamily(Os.FAMILY_MAC)) {
                commandLine "/usr/local/bin/node", "/usr/local/bin/webpack", "--config", "webpack.config.js"
            } else {
                commandLine "webpack", "--config", "webpack.config.js"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.ext.grp
            artifactId 'weavergirl-core'
            version rootProject.ext.ver

            artifact(file("${buildDir}/weavergirl/${project.name}-${rootProject.ext.ver}.jar")) {
                builtBy makeWeavergirlJar
            }
        }
    }
}

project.afterEvaluate {
    webpack.dependsOn(compileTypeScript)
    makeWeavergirlApp.dependsOn(webpack)
}