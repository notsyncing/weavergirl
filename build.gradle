buildscript {
    ext.kotlin_version = '1.1.0-beta-17'

    repositories {
        mavenCentral()

        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

project.ext {
    grp = "io.github.notsyncing.weavergirl"
    ver = '0.1.0'
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "kotlin2js"

    def ver = rootProject.ext.ver
    def grp = rootProject.ext.grp

    group grp
    version ver

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }

        maven {
            url "https://dl.bintray.com/notsyncing/maven/"
        }

        jcenter()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-js-library:$kotlin_version"
        compile "io.github.notsyncing.kotlin.js.ext:kotlin-js-ext:0.1.0"
    }

    def outputDir = "${projectDir}/../out/production"

    compileKotlin2Js {
        kotlinOptions {
            outputFile = "${outputDir}/${project.name}.js"
            metaInfo = true
            sourceMap = true
        }
    }

    compileTestKotlin2Js {
        kotlinOptions {
            outputFile = "${outputDir}/../test/${project.name}.js"
            metaInfo = true
            sourceMap = true
        }
    }

    task publishJar(type: Jar) {
        from outputDir
        include("${project.name}.js", "${project.name}.meta.js", "${project.name}/**")
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier "sources"
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier "javadoc"
        from javadoc.destinationDir
    }

    uploadArchives {
        repositories {
            mavenLocal()
        }
    }
}
